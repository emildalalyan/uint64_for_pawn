#### [<< главная](../Main.md)
## Преобразования из других типов данных
В библиотеке существует несколько функций для преобразования из других типов данных.

### Преобразование из строки
```pawn
stock uint64fromstr(dest[uint64], const string[])
```

Преобразовывает строку из <code>string[]</code> и записывает значение в переменную <code>dest</code>.

#### Примечание
Число во входной строке должно быть записано в десятичной системе счисления.

#### Пример использования
```pawn
main()
{
    new number[uint64];

    uint64fromstr(number, "500 000 000 000");
    // ^^^ после этой строки в переменной number
    // лежит значение 500 000 000 000
}
```

Функция игнорирует любые символы в строке кроме цифр. Таким образом, например, строка *"500,000,000,000"* тоже преобразуется корректно.

### Преобразование из обычного 32-битного числа
```pawn
stock uint64from32(dest[uint64], value)
```

Преобразовывает обычное число <code>value</code> в число uint64 и записывает в переменную <code>dest</code>.

#### Пример использования
```pawn
main()
{
    new number[uint64];

    uint64from32(number, 5000000);
    // ^^^ после этой строки в переменной number
    // лежит значение 5 000 000
}
```
<hr>

## Преобразование из uint64 в другие типы данных
На данный момент существует лишь одна функция для преобразования из uint64.

### Преобразование в строку
```pawn
stock uint64tostr(str[], length, const num[uint64], putspaces = true)
```

Функция преобразовывает число <code>num[uint64]</code> в строку и записывает результат в <code>str[]</code> с длиной <code>length</code>.

Параметр <code>putspaces</code> отвечает за вставку пробелов перед разрядами кратных трём, т.е тысяч, миллионов, миллиардов, и т.п. К примеру, строка с выключенным параметром будет выглядить так: *897345678456*, а со включенным так: *897 345 678 456*.

По умолчанию, параметр <code>putspaces</code> включен для лучшей читаемости больших чисел.

#### Примечание
Число в конечной строке будет записано в десятичной системе счисления.

#### Пример использования
```pawn
main()
{
    new number[uint64];
    uint64fromstr(number, "500 000 000 000");
    // ^^^ кладем в переменную значение 500 миллиардов

    new outputstr[128];
    uint64tostr(outputstr, sizeof(outputstr), number);
    // ^^^ преобразовываем число в строку и кладем результат в outputstr

    print(outputstr);
    // ^^^ выводим получившуюся строку в консоль
}
```

### Преобразование в обычное 32-битное значение
Для этого в библиотеке не предусмотрено функций, однако и без таковой сделать это не составит труда.

Преобразование в 32-битное значение возможно, **если число заведомо меньше** максимального допустимого *2 147 483 647*, иначе результат может быть некорректным.

Для этого необходимо просто обратится к массиву по нужному индексу, вот пример такого преобразования:

```pawn
main()
{
    new number[uint64];
    uint64fromstr(number, "500 000");
    // ^^^ кладем в переменную значение 500 тысяч

    new num32;
    num32 = number[uint64_lowhalf];
    // ^^^ обращаемся к нужному индексу массива и
    // теперь в переменной num32 лежит значение
    // из числа number.

    printf("%i", num32);
    // ^^^ выводим число на экран.
}
```